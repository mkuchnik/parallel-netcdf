AC_REVISION($Id$)dnl
dnl
dnl Process this file with GNU autoconf(1) to produce a configure script.
dnl

dnl Defaults:
CPPFLAGS=${CPPFLAGS--DNDEBUG}
CFLAGS=${CFLAGS--O}
FPPFLAGS=${FPPFLAGS-}
FFLAGS=${FFLAGS-}

AC_INIT(src/lib/ncconfig.in)
UD_SRCDIR
AC_PREFIX_DEFAULT(`(cd ..; pwd)`)

AC_CONFIG_HEADER(src/lib/ncconfig.h:src/lib/ncconfig.in)

UD_PROG_M4
AC_PROG_CC
AC_PROG_CXX
AC_PROG_F77
dnl UD_PROG_CC
UD_PROG_CC_MAKEDEPEND
dnl need a --with-cxx=no and --with-fortran=no
dnl UD_PROG_CXX
dnl UD_PROG_FC
dnl UD_PROG_F90
UD_FORTRAN_TYPES
UD_CHECK_LIB_MATH
UD_PROG_AR()
UD_PROG_NM()
AC_PROG_RANLIB

MPICC=$CC
AC_MSG_CHECKING(looking for mpi implementation)
AC_ARG_WITH(mpi, 
[  --with-mpi              installation prefix for mpi implementation],
	if test x$withval = xyes; then
		AC_MSG_ERROR(--with-mpi must be given a pathname)
	else
		AC_MSG_RESULT(using $withval)
		MPICC=${withval}/bin/mpicc
		if test ! -x "${MPICC}"; then
			AC_MSG_WARN($MPICC either does not exist or is not executable: falling back to $CC)
			MPICC="$CC"
		fi
	fi
)


AC_MSG_CHECKING(looking for MPI-IO support in MPI implementation)
saveCC="$CC"
CC=$MPICC
AC_TRY_COMPILE([#include "mpi.h"], [int a = MPI_MODE_RDWR],
	AC_MSG_RESULT(yes),
	AC_MSG_ERROR([mpi implementation does not support MPI-IO])
)
CC="$saveCC"
AC_SUBST(MPICC)


AC_CHECK_HEADER(stdlib.h, ,AC_DEFINE(NO_STDLIB_H))
AC_CHECK_HEADER(sys/types.h, ,AC_DEFINE(NO_SYS_TYPES_H))
AC_CHECK_FUNC(strerror, ,AC_DEFINE(NO_STRERROR))
UD_CHECK_FTRUNCATE
AC_FUNC_ALLOCA
AC_STRUCT_ST_BLKSIZE
UD_CHECK_IEEE
AC_TYPE_SIZE_T
AC_TYPE_OFF_T
AC_CHECK_TYPE(ssize_t, int)
AC_CHECK_TYPE(ptrdiff_t, int)
AC_CHECK_TYPE(uchar, unsigned char)
AC_C_CHAR_UNSIGNED
AC_C_BIGENDIAN
AC_CHECK_SIZEOF(short)
AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(long)
AC_CHECK_SIZEOF(float)
AC_CHECK_SIZEOF(double)
UD_CHECK_SIZEOF(off_t)
UD_CHECK_SIZEOF(size_t)
UD_MAKEWHATIS
UD_FTPBINDIR
AC_OUTPUT(macros.make Makefile src/Makefile test/Makefile src/lib/Makefile test/common/Makefile test/test_double/Makefile test/test_double_int/Makefile test/test_float/Makefile test/test_int/Makefile)
