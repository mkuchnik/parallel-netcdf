AC_REVISION($Id$)dnl
dnl -*- Mode: shell-script-mode; -*-
dnl Process this file with GNU autoconf(1) to produce a configure script.
dnl

AC_PREREQ(2.59)
AC_INIT([PnetCDF-FLASH-IO], [1.0.0], [parallel-netcdf@mcs.anl.gov])

dnl user defined macro for printing messages for debugging
_DEBUG=no
AC_DEFUN([UD_MSG_DEBUG],
         [if test "x${_DEBUG}" = xyes ; then
             AC_MSG_NOTICE(DEBUG: $1)
          fi
         ]
)

dnl an option to use a customized rm command
if test "x${RM}" != x ; then
   AC_MSG_CHECKING(rm )
   if ! test -f ${RM} ; then
      AC_CHECK_PROG([rm_cmd], [${RM}], [yes], [no])
      if test "x${rm_cmd}" = xyes ; then
         RM=${RM}
      fi
   else
      RM=${RM}
   fi
   AC_MSG_RESULT(using $RM)
else
   RM="rm"
fi
AC_SUBST(RM)

AC_ARG_ENABLE(echo,
   [AS_HELP_STRING([--enable-echo],
                   [Turn on strong echoing. @<:@default: no@:>@])],
   [set -x]
)

AC_ARG_VAR(MPIF90, MPI Fortran 90 compiler)
# Check env variable $MPIF90 set by the users
if test "x${MPIF90}" != x ; then
    UD_MSG_DEBUG(MPIF90 is set to $MPIF90)
    if test ! -x "${MPIF90}" ; then
        UD_MSG_DEBUG(MPIF90 is defined as ${MPIF90} but file does not exist)
        # MPIF90 may not be an absolute path, check MPIF90 from $PATH
        MPIF90_ISVALID=""
        AC_PATH_PROGS(MPIF90_ISVALID, [${MPIF90}])
        if test "x${MPIF90_ISVALID}" = x ; then
            AC_MSG_WARN(File ${MPIF90} does not exist)
            MPIF90=""
        else
            MPIF90=$MPIF90_ISVALID
        fi
    fi
fi

dnl if MPIF90 is still empty, search under user's PATH
CANDIDATE_MPIF90="mpif90 mpixlf90_r mpixlf90 blrts_xlf90 mpxlf90_r mpxlf90 mpxlf95_r mpxlf95 ftn mpiifort mpiifc"
if test "x${MPIF90}" = x ; then
   UD_MSG_DEBUG(find possible MPIF90 under user's PATH)
   AC_PATH_PROGS([MPIF90], [$CANDIDATE_MPIF90])
fi

if test "x${MPIF90}" = x ; then
   AC_MSG_ERROR([
   -----------------------------------------------------------------------
    No MPI F90 compiler can be found. FLASH I/O kernel requires an MPI F90
    compiler. Please specify the location of one with the MPIF90 environment
    variable
   -----------------------------------------------------------------------])
fi

dnl Check if MPIF90 is a valid MPI compiler
FC=${MPIF90}
AC_PROG_FC

dnl steal from autoconf 2.69
AC_DEFUN([UD_FC_PP_SRCEXT],
[AC_LANG_PUSH(Fortran)dnl
AC_CACHE_CHECK([for Fortran flag to compile preprocessed .$1 files],
                ac_cv_fc_pp_srcext_$1,
[ac_ext=$1
ac_fcflags_pp_srcext_save=$ac_fcflags_srcext
ac_fcflags_srcext=
ac_cv_fc_pp_srcext_$1=unknown
case $ac_ext in #(
  [[fF]]77) ac_try=f77-cpp-input;; #(
  *) ac_try=f95-cpp-input;;
esac
for ac_flag in none -ftpp -fpp -Tf "-fpp -Tf" -xpp=fpp -Mpreprocess "-e Z" \
               -cpp -xpp=cpp -qsuffix=cpp=$1 "-x $ac_try" +cpp -Cpp; do
  test "x$ac_flag" != xnone && ac_fcflags_srcext="$ac_flag"
  AC_COMPILE_IFELSE([AC_LANG_PROGRAM([], [[
#if 0
#include <ac_nonexistent.h>
      choke me
#endif]])],
    [AC_COMPILE_IFELSE([AC_LANG_PROGRAM([], [[
#if 1
#include <ac_nonexistent.h>
      choke me
#endif]])],
       [],
       [ac_cv_fc_pp_srcext_$1=$ac_flag; break])])
done
rm -f conftest.$ac_objext conftest.$1
ac_fcflags_srcext=$ac_fcflags_pp_srcext_save
])
if test "x$ac_cv_fc_pp_srcext_$1" = xunknown; then
  m4_default([$3],
             [AC_MSG_ERROR([Fortran could not compile preprocessed .$1 files])])
else
  ac_fc_srcext=$1
  if test "x$ac_cv_fc_pp_srcext_$1" = xnone; then
    ac_fcflags_srcext=""
    FCFLAGS_[]$1[]=""
  else
    ac_fcflags_srcext=$ac_cv_fc_pp_srcext_$1
    FCFLAGS_[]$1[]=$ac_cv_fc_pp_srcext_$1
  fi
  AC_SUBST(FCFLAGS_[]$1)
  $2
fi
AC_LANG_POP(Fortran)dnl
])# UD_FC_PP_SRCEXT

dnl find compile flags for handling files with .F .F90 .f90 extensions
dnl steal AC_FC_PP_SRCEXT from autoconf V2.69 to make UD_FC_PP_SRCEXT
UD_FC_PP_SRCEXT([F90])
AC_SUBST(FCFLAGS_F90)

FPPFLAGS=${FPPFLAGS-}
AC_SUBST(FPPFLAGS)

dnl steal from autoconf 2.69
AC_DEFUN([UD_FC_PP_DEFINE],
[AC_LANG_PUSH([Fortran])dnl
ac_fc_pp_define_srcext_save=$ac_fc_srcext
ac_ext_saved=$ac_ext
UD_FC_PP_SRCEXT([F])
ac_ext=F
AC_CACHE_CHECK([how to define symbols for preprocessed Fortran],
  [ac_cv_fc_pp_define],
[ac_fc_pp_define_srcext_save=$ac_fc_srcext
ac_cv_fc_pp_define=unknown
ac_fc_pp_define_FCFLAGS_save=$FCFLAGS
for ac_flag in -D -WF,-D -Wp,-D -Wc,-D
do
  FCFLAGS="$ac_fc_pp_define_FCFLAGS_save ${ac_flag}FOOBAR ${ac_flag}ZORK=42"
  AC_COMPILE_IFELSE([AC_LANG_PROGRAM([], [[
#ifndef FOOBAR
      choke me
#endif
#if ZORK != 42
      choke me
#endif]])],
    [ac_cv_fc_pp_define=$ac_flag])
  test x"$ac_cv_fc_pp_define" != xunknown && break
done
FCFLAGS=$ac_fc_pp_define_FCFLAGS_save
])
ac_fc_srcext=$ac_fc_pp_define_srcext_save
if test "x$ac_cv_fc_pp_define" = xunknown; then
  FC_DEFINE=
  m4_default([$2],
             [AC_MSG_ERROR([Fortran does not allow to define preprocessor symbols], 77)])
else
  FC_DEFINE=$ac_cv_fc_pp_define
  $1
fi
ac_ext=$ac_ext_saved
AC_SUBST([FC_DEFINE])dnl
AC_LANG_POP([Fortran])dnl
])

dnl compiler command-line define preprocess flag, result in FC_DEFINE
UD_FC_PP_DEFINE

AC_LANG_PUSH([Fortran])
AC_FC_SRCEXT([f90])
AC_SUBST(FCFLAGS_f90)

AC_COMPILE_IFELSE([AC_LANG_CALL([],[MPI_Comm_rank])],
                  [valid_mpif90=yes],[valid_mpif90=no]
)
AC_LANG_POP([Fortran])
if test "x${valid_mpif90}" = xno && test "x${enable_fortran}" = xyes ; then
   AC_MSG_ERROR([
      ------------------------------------------------------------
        Invalid MPI Fortran 90 compiler specified: "${MPIF90}"
        A working MPI compiler is required. Please specify the
        location of one either with the MPIF90 environment
        variable or the --with-mpi configure flag
      ------------------------------------------------------------])
fi
AC_SUBST(MPIF90)
FC=$MPIF90

PNETCDF_INSTALL=""
AC_ARG_WITH(pnetcdf,
    [AS_HELP_STRING([--with-pnetcdf=/path/to/implementation],
                    [installation prefix for PnetCDF implementation])],
    if test "x${withval}" = xyes; then
        AC_MSG_ERROR(--with-pnetcdf must be given a pathname)
    else
        PNETCDF_INSTALL=${withval}
    fi
)

AC_MSG_CHECKING(PnetCDF Fortran library)
PNETCDF_INC=${PNETCDF_INSTALL}/include
PNETCDF_LIB=${PNETCDF_INSTALL}/lib

CPPFLAGS+=" -I${PNETCDF_INC}"
LDFLAGS+=" -L${PNETCDF_LIB} -lpnetcdf"

have_pnetcdf=no

AC_LANG_PUSH([Fortran])
AC_LINK_IFELSE([AC_LANG_CALL([], [nfmpi_create])],
   [AC_MSG_RESULT(works)
    have_pnetcdf=yes],
   [AC_MSG_RESULT(failed to compile test program)
    unset FC
   ]
)
AC_LANG_POP([Fortran])
AC_SUBST(PNETCDF_INC)
AC_SUBST(PNETCDF_LIB)

if test "x${have_pnetcdf}" = xno; then
    AC_MSG_ERROR([
    ------------------------------------------------------------
     The PnetCDF library and header file are required to build
     the GCRM I/O kernel. Use option
         --with-pnetcdf=/path/to/implementation
     to specify the location of PnetCDF build.  Stopping ...
     Check 'config.log' for more information.
    ------------------------------------------------------------])
fi

AC_CONFIG_FILES(Makefile)
AC_OUTPUT
echo "--------------------------------------------------------------------"
echo \
"
   FLASH I/O kernel with PnetCDF I/O method

   Compilers: 
              MPIF90      = ${MPIF90}
              FFLAGS      = ${FFLAGS}
              FCFLAGS     = ${FCFLAGS}
              F90FLAGS    = ${F90FLAGS}
              LDFLAGS     = ${LDFLAGS}
              LIBS        = ${LIBS}

   Now type 'make' to produce executable
---------------------------------------------------------------------"

